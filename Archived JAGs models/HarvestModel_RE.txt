model{
# Hyperpriors for random year effects
mu.phi ~ dnorm(0, 0.1)
sd.phi ~ dunif(0, 5)
tau.phi <- pow(sd.phi, -2)

mu.gamma ~ dnorm(0, 0.1)
sd.gamma ~ dunif(0, 5)
tau.gamma <- pow(sd.gamma, -2)

# Random year effects for alpha intercepts
for (k in 1:nyear) {
    alpha.phi[k] ~ dnorm(mu.phi, tau.phi)
    alpha.gamma[k] ~ dnorm(mu.gamma, tau.gamma)
}

# Priors for delta coefficients
delta.phi[1] ~ dnorm(0, 0.01)
delta.gamma[1] ~ dnorm(0, 0.01)
delta.phi[2] ~ dnorm(0, 100)  # Very small variance for no harvest
delta.gamma[2] ~ dnorm(0, 100)  # Very small variance for no harvest

# Priors for Psi (Occupancy)
for(i in 1:nbeta.psi) {
    beta.psi[i] ~ dnorm(0, 0.01)
}

# Priors for Phi and Gamma betas for harvest vs. no harvest
for(i in 1:nbeta.phi) {
    for(k in 1:2) {
        beta.phi[i, k] ~ dnorm(0, 0.01)
    }
}

for(i in 1:nbeta.gamma) {
    for(k in 1:2) {
        beta.gamma[i, k] ~ dnorm(0, 0.01)
    }
}

# Priors for P (Detection Probability)
for(i in 1:nbeta.p) {
    beta.p[i] ~ dnorm(0, 0.01)  # Detection  
}

# Ecological sub-model
for(i in 1:nsite) {
    logit(psi[i]) <- inprod(beta.psi, x.psi[i, ])
    z[i, 1] ~ dbern(psi[i])
    
    for(k in 2:nyear) {
        logit(phi[i, k - 1]) <- alpha.phi[k] + delta.phi[harvest[i, k]]*harvest_age[i, k] + inprod(beta.phi[, harvest[i, k]], x.phi.harvest[i, k, ])
        logit(gamma[i, k - 1]) <- alpha.gamma[k] + delta.gamma[harvest[i, k]]*harvest_age[i, k] + inprod(beta.gamma[, harvest[i, k]], x.gamma.harvest[i, k, ])
        
        muZ[i, k] <- z[i, k - 1]*phi[i, k - 1] + (1 - z[i, k - 1])*gamma[i, k - 1]
        z[i, k] ~ dbern(muZ[i, k])
    }
}

# Detection sub-model
for(i in 1:nsite) {
    for(k in 1:nyear) {
        for(j in 1:J[i, k]) {
            logit(p[i, k, j]) <- inprod(beta.p, x.p[i, k, j, ])
            muy[i, k, j]  <- z[i, k]*p[i, k, j]
            y[i, k, j] ~ dbern(muy[i, k, j])
        }
    }
}


# Derived variable for the number of sites occupied each year
for(k in 1:nyear) {
    N[k] <- sum(z[,k])
   }
}