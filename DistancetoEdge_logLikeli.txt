model{

  # Priors 
  # Psi 
  for(i in 1:nbeta.psi){
    beta.psi[i] ~ dnorm(0, 0.01)
  }
  
  # Phi
  for(i in 1:nbeta.phi){
    beta.phi[i] ~ dnorm(0, 0.01)
  }
  
  # Gamma 
  for(i in 1:nbeta.gamma){ 
    beta.gamma[i] ~ dnorm(0, 0.01)
  }

  # P 
  for(i in 1:nbeta.p){
    beta.p[i] ~ dnorm(0, 0.01)  # Detection 
  }

  # Ecological sub-model 
  for(i in 1:nsite){
    logit(psi[i]) <- inprod(beta.psi, x.psi[i, ])
    z[i, 1] ~ dbern(psi[i])
    
    for(k in 2:nyear){
      logit(phi[i, k - 1]) <- inprod(beta.phi, x.phi[i, k, ])
      logit(gamma[i, k - 1]) <- inprod(beta.gamma, x.gamma[i, k, ])
      
       muZ[i, k] <- z[i, k - 1] * phi[i, k - 1] + (1 - z[i, k - 1]) * gamma[i, k - 1] 
      z[i, k] ~ dbern(muZ[i, k])
  
   # Calculate turnover (tau)
      tau[i, k-1] <- gamma[i, k - 1] * (1 - z[i, k - 1]) /
                   (gamma[i, k - 1] * (1 - z[i, k - 1]) + (1 - phi[i, k - 1]) * z[i, k - 1])
    }
  }
  
  
  # Detection sub-model
  for(i in 1:nsite){
    for(k in 1:nyear){
      for(j in 1:J[i, k]){
        logit(p[i, k, j]) <- inprod(beta.p, x.p[i, k, j, ])
        muy[i, k, j] <- z[i, k] * p[i, k, j]
        y[i, k, j] ~ dbern(muy[i, k, j])
      }
    }
  }  

# Log likelihood calculation
for(k in 1:nyear) {
    for(i in 1:nsite) {
        for(j in 1:J[i, k]) {
            # Probability of observing y[i, k, j] given p[i, k, j]
            prob1[i, k, j] <- pow(p[i, k, j], y[i, k, j]) * pow((1 - p[i, k, j]), (1 - y[i, k, j]))
            
            # Probability of not detecting species given site is occupied
            prob2[i, k, j] <- 1 - p[i, k, j]
        }

        # Likelihood term if species is detected at least once at the site in year k
        term1[i, k] <- ind[i, k] * muZ[i, k] * prod(prob1[i, k, 1:J[i, k]])
        
        # Likelihood term if species is never detected at the site in year k
        term2[i, k] <- (1 - ind[i, k]) * ((1 - muZ[i, k]) + muZ[i, k] * prod(prob2[i, k, 1:J[i, k]]))
        
        # Log likelihood for site i in year k
        y.prob[i, k] <- term1[i, k] + term2[i, k]
        lprob.y[i, k] <- log(y.prob[i, k])
    }

    # Sum of log likelihoods across all sites for year k
    score.year[k] <- sum(lprob.y[1:nsite, k])
}

# Total log likelihood (Deviance)
l.score <- -2 * sum(score.year[1:nyear])

  # Derived variable
  for(k in 1:nyear) {
    N[k] <- sum(z[,k]) # no. sites occupied for each year
  }
}